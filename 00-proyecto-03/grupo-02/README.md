# grupo-02 - Gatolate ‚ÇäÀö‚äπ·∞î

**Integrantes:**  
- 01-antokiaraa
- 16-javieramoraga-rgb
- 18-ppia97

## Introducci√≥n .‚òòÔ∏é ›ÅÀñ

Este proyecto surge desde la idea de crear **m√°quinas emocionales**, es decir, dispositivos que no buscan cumplir una funci√≥n pr√°ctica, sino expresar una emoci√≥n a trav√©s del comportamiento f√≠sico: movimiento, luz, ritmo y tiempo.

Durante el taller, experimentamos con la idea de transformar emociones en acciones corporales de una m√°quina. En lugar de explicarlas con palabras, exploramos c√≥mo una emoci√≥n puede **manifestarse f√≠sicamente**, como ocurre en una m√°quina que saluda, o un poema que reacciona al entorno.

Siguiendo esa l√≥gica, dise√±amos dos peque√±as m√°quinas que reaccionan a la presencia del usuario como si **sintieran algo**, sin hablar, solo comport√°ndose.

Elegimos dos emociones silenciosas, dif√≠ciles de verbalizar, pero muy evidentes cuando se observan en un cuerpo: **Ansiedad** y **Verg√ºenza.**


## Significado de emociones .‚òòÔ∏é ›ÅÀñ

### ‚§∑ Ansiedad:
> Es un estado de **anticipaci√≥n y alerta constante**. No siempre ocurre algo, pero el cuerpo act√∫a como si algo estuviera por pasar. Se manifiesta en forma de tensi√≥n, latido r√°pido, vibraci√≥n o inquietud permanente. No descansa del todo.

### ‚§∑ Verg√ºenza:
> Es el sentimiento que aparece cuando uno **se siente observado**. El cuerpo solo quiere esconderse, apagarse o evitar la mirada. No busca llamar la atenci√≥n, sino desaparecer.


Estas emociones casi nunca se explican hablando; se expresan **con gestos, posturas y reacciones corporales**. Por eso, nuestras m√°quinas **no hablan, se comportan.**



### M√°quina de la Ansiedad ·µé·µé

Representa un cuerpo que **nunca consigue estar completamente quieto**.

- Vibra constantemente, incluso cuando no hay nadie cerca (motor N20 controlado con MOSFET).
- Cuando detecta aproximaci√≥n (sensor ultras√≥nico), la vibraci√≥n aumenta y el LED late m√°s r√°pido, simulando un coraz√≥n nervioso.
- Al alejarse la persona, no vuelve inmediatamente a la calma: mantiene vibraciones leves, simulando el *residuo de tensi√≥n*.


‚îÄ‚îÄ‚îÄ‚îÄ ‡≠®‡≠ß ‚îÄ‚îÄ‚îÄ‚îÄ


### M√°quina de la Verg√ºenza ·µé·µé
Representa un cuerpo que **prefiere no ser observado**.

- Cuando no hay nadie cerca, permanece encendida con luz suave y movimientos muy leves.
- Si alguien se acerca, la m√°quina apaga su LED y gira repentinamente con el servomotor, como un gesto de evasi√≥n.
- No vuelve a activarse inmediatamente. Espera antes de volver a mostrarse, como quien necesita tiempo para recuperarse despu√©s de sentirse observado.



### Relaci√≥n emocional entre ambas .‚òòÔ∏é ›ÅÀñ

Aunque funcionan por separado, comparten una l√≥gica emocional:

| Situaci√≥n | Reacci√≥n esperada |
|-----------|--------------------|
| La verg√ºenza se esconde | La ansiedad aumenta |
| La ansiedad se agita | La verg√ºenza se intensifica |

> No conversan entre s√≠, pero **se afectan emocionalmente**, igual que las personas.


## Planificaci√≥n y proceso previo ìè≤ ‡πã‡£≠ ‡£™ Àñüéê

Durante el proyecto, utilizamos una planificaci√≥n modular por etapas. Aunque la Carta Gantt fue dise√±ada inicialmente, el cronograma fue ajust√°ndose, ya que algunas decisiones t√©cnicas (como el cambio de motor y uso de MOSFET) surgieron en etapas avanzadas.

![cartagantt](./imagenes/cartagantt.jpeg)

### Etapas de trabajo

**1. Exploraci√≥n conceptual**
- Investigaci√≥n sobre emociones y c√≥mo se manifiestan f√≠sicamente.
- Selecci√≥n de Ansiedad y Verg√ºenza como emociones principales.
- Primeras ideas sobre c√≥mo traducirlas en gestos mec√°nicos (temblor, giro, luz).

**2. Definici√≥n de componentes y posibles comportamientos**
- B√∫squeda de actuadores que representaran f√≠sicamente cada emoci√≥n.
- Verg√ºenza ‚Üí servomotor (giro evasivo) y LED que se apaga.
- Ansiedad ‚Üí motor vibrador (luego reemplazado por motor N20 con MOSFET) y LED con pulso irregular.

**3. Primer prototipo t√©cnico**
- Pruebas individuales de cada componente: sensor ultras√≥nico, servo, LED, motor vibrador.
- Primera programaci√≥n b√°sica de reacci√≥n a distancia.
- Problemas con la intensidad del motor vibrador ‚Üí se detecta que no es suficiente.

**4. Redefinici√≥n t√©cnica y compra de componentes**
- Se decide reemplazar el motor vibrador por un **micromotor N20** m√°s potente.
- Se integra **MOSFET IRF520** (o equivalente) para controlar el motor con PWM.
- Ajustes en el dise√±o del c√≥digo y reorganizaci√≥n del circuito.

**5. Integraci√≥n de comportamientos emocionales**
- Ansiedad: implementaci√≥n de vibraci√≥n basal y vibraci√≥n err√°tica seg√∫n cercan√≠a.
- Verg√ºenza: programar apagado de color y giro evasivo.
- Se incorpora ‚Äúresiduo emocional‚Äù: tiempo de espera antes de calmarse.

**6. Montaje f√≠sico**
- Impresi√≥n 3D de piezas y pruebas de ensamblaje (cabeza de gatito para el ultras√≥nico).
- Consideraci√≥n de caja como escondite emocional.
- Problemas iniciales con medidas y material (resina vs filamento).

**7. Pruebas de interacci√≥n y ajustes**
- Ajustes manuales de √°ngulos del servo.
- Ajuste de distancias m√≠nimas y m√°ximas para que cada m√°quina reaccione como se espera.
- Verificaci√≥n de respuesta emocional (tiempo de reacci√≥n, intensidad, latencia).

**8. Documentaci√≥n y preparaci√≥n de entrega**
- Organizaci√≥n de bit√°coras individuales.
- Redacci√≥n del informe final (este documento).
- Preparaci√≥n de exposici√≥n.


‚îÄ‚îÄ‚îÄ‚îÄ ‡≠®‡≠ß ‚îÄ‚îÄ‚îÄ‚îÄ


### Decisiones clave que modificaron el proyecto

| Problema | Cambio realizado |
|----------|------------------|
| Motor vibrador no ten√≠a fuerza | Se reemplaza por motor N20 + MOSFET |
| Biblioteca NewPing incompatibilidad | Se decide usar l√≥gica est√°ndar de duraci√≥n de pulso |
| Carcasa en resina r√≠gida sin movilidad | Se decide pasar a impresi√≥n en filamento |
| Intensidad de gir muy brusca | Se ajusta PWM y delay para suavizar la respuesta |


‚îÄ‚îÄ‚îÄ‚îÄ ‡≠®‡≠ß ‚îÄ‚îÄ‚îÄ‚îÄ


## Pseudoc√≥digo Explicado ‚ÇäÀö‚äπ·∞î

### Componentes utilizados 

| M√°quina   | Componentes principales | Funci√≥n emocional                         |
|-----------|------------------------|-------------------------------------------|
| Ansiedad  | Sensor ultras√≥nico, motor DC N20 con MOSFET, LED RGB | Detecta cercan√≠a, vibra, simula ritmo card√≠aco |
| Verg√ºenza | Sensor ultras√≥nico, servomotor SG90, LED RGB | Detecta observaci√≥n, se gira y apaga |



### Pseudoc√≥digo ‚Äî M√°quina Ansiedad ‚ö°Ô∏é

```plaintext
INICIO

Definir pinSensorTrig = 2
Definir pinSensorEcho = 3
Definir pinLED = 9
Definir pinMotor = 5

Configurar pines como entrada o salida

Mientras la m√°quina est√° encendida:

  Leer distancia usando sensor ultras√≥nico

  Si distancia < 30 cm:
      // Ansiedad activa
      Aumentar PWM del motor (vibraci√≥n fuerte)
      Aumentar velocidad de parpadeo del LED
      Guardar √∫ltimo tiempo en que se detect√≥ presencia
  
  Sino si distancia >= 30 cm Y (tiempo desde la √∫ltima detecci√≥n < tiempo de residuo):
      // Ansiedad residual
      Mantener vibraci√≥n leve (PWM medio-bajo)
      LED parpadea lento
  
  Sino:
      // Estado basal
      Vibraci√≥n muy leve
      LED mantiene pulso lento

Fin

```


### Pseudoc√≥digo ‚Äî M√°quina Verg√ºenza ‚ö°Ô∏é

```plaintext
INICIO

Definir pinSensorTrig = 2
Definir pinSensorEcho = 3
Definir pinServo = 6
Definir pinLED = 9

Configurar pines

Mientras la m√°quina est√° encendida:

  Leer distancia desde sensor ultras√≥nico

  Si distancia < 30 cm:
      // Se siente observada ‚Üí se esconde
      Apagar LED
      Servo gira a posici√≥n de ocultamiento (ej. 120¬∞)
      Guardar √∫ltimo tiempo de detecci√≥n
  
  Sino si distancia >= 30 cm Y (tiempo desde la √∫ltima detecci√≥n < tiempo de recuperaci√≥n):
      // Sigue escondida aunque ya no la miren
      Mantener LED apagado
      Mantener servo girado
  
  Sino:
      // Nadie la observa ‚Üí t√≠mida pero presente
      Encender LED con brillo suave
      Servo vuelve lentamente a posici√≥n inicial (ej. 0¬∞)

Fin
```

‚îÄ‚îÄ‚îÄ‚îÄ‡≠®‡ßé‚îÄ‚îÄ‚îÄ‚îÄ


### Explicaci√≥n del comportamiento emocional ìè≤ ‡πã‡£≠ ‡£™ Àñüéê

| Estado del entorno | M√°quina Ansiedad | M√°quina Verg√ºenza |
|--------------------|------------------|-------------------|
| No hay nadie cerca | Vibra muy poco, LED lento (estado basal) | Luz tenue, servo centrado |
| Persona se acerca  | Vibra fuerte y LED acelera (alerta) | Apaga LED y se gira para esconderse |
| Persona se aleja   | Disminuye vibraci√≥n lentamente (residuo emocional) | Espera antes de volver a mostrarse (recuperaci√≥n emocional) |



### Idea central ‡πã ‡£≠ ‚≠ë

> Estas m√°quinas no cambian de estado inmediatamente.  
> Imitan el comportamiento emocional humano:  
> **las emociones tienen latencia, residuo y recuperaci√≥n.**


‚îÄ‚îÄ‚îÄ‚îÄ‡≠®‡ßé‚îÄ‚îÄ‚îÄ‚îÄ



## Diagrama de Flujo ‚Äî M√°quina Ansiedad ‚ö°Ô∏é

```mermaid
flowchart TD
    A[Inicio] --> B[Leer distancia del sensor]

    B --> C{Distancia < 30cm?}

    C -- Si --> D[Ansiedad activa]
    D --> E[Vibraci√≥n fuerte]
    E --> F[LED r√°pido]
    F --> G[Guardar tiempo de detecci√≥n]
    G --> B

    C -- No --> H{Dentro del tiempo residual?}

    H -- Si --> I[Ansiedad residual]
    I --> J[Vibraci√≥n leve]
    J --> K[LED lento]
    K --> B

    H -- No --> L[Estado basal]
    L --> M[Vibraci√≥n m√≠nima]
    M --> N[LED suave]
    N --> B
```


## Diagrama de Flujo ‚Äî M√°quina Verg√ºenza ‚ö°Ô∏é

```mermaid
flowchart TD
    A[Inicio] --> B[Leer distancia del sensor]

    B --> C{Distancia < 30cm?}

    C -- Si --> D[Se activa verg√ºenza]
    D --> E[Apagar LED]
    E --> F[Girar servo para esconderse]
    F --> G[Guardar tiempo de detecci√≥n]
    G --> B

    C -- No --> H{Dentro tiempo de recuperaci√≥n?}

    H -- Si --> I[Mantener escondida]
    I --> J[LED apagado]
    J --> K[Servo en posici√≥n oculta]
    K --> B

    H -- No --> L[Estado normal]
    L --> M[Encender LED suave]
    M --> N[Servo vuelve al inicio]
    N --> B

```

‚îÄ‚îÄ‚îÄ‚îÄ‡≠®‡ßé‚îÄ‚îÄ‚îÄ‚îÄ

## Bill of Materials (BOM) ‡πã ‡£≠ ‚≠ë

## M√°quina Ansiedad ‚Äî Componentes ‚ãÜ êôö Ãä.

| Componente | Cantidad | Uso t√©cnico | Rol emocional |
|-----------|----------|-------------|----------------|
| Arduino UNO / Nano | 1 | Microcontrolador principal | Gestiona los estados de ansiedad |
| Sensor ultras√≥nico HC-SR04 | 1 | Detecci√≥n de distancia | Detecta cercan√≠a como ‚Äúalerta‚Äù |
| Motor DC N20 | 1 | Genera vibraci√≥n | Representa temblor/agitamiento |
| M√≥dulo MOSFET | 1 | Control de potencia del motor mediante PWM | Intensidad del ‚Äútemblor emocional‚Äù |
| LED RGB | 1 | Se√±al luminosa | Representa latido ansioso |
| Resistencias 220‚Äì330Œ© | 2‚Äì3 | Protegen el LED | ‚Äî |
| Cables MM - MH | varios | Conexi√≥n | ‚Äî |
| Protoboard | 1 | Ensamble | ‚Äî |



## M√°quina Verg√ºenza ‚Äî Componentes ‚ãÜ êôö Ãä.

| Componente | Cantidad | Uso t√©cnico | Rol emocional |
|-----------|----------|-------------|----------------|
| Arduino UNO / Nano | 1 | Microcontrolador | Controla estados de verg√ºenza |
| Sensor ultras√≥nico HC-SR04 | 1 | Detecta presencia | ‚ÄúSe siente observada‚Äù |
| Servo SG90 | 1 | Movimiento rotacional | Se gira para esconderse |
| LED RGB | 1 | Iluminaci√≥n | Se apaga en verg√ºenza |
| Resistencias | varias | Protegen LEDs | ‚Äî |
| Cables MM - MH | varios | Conexi√≥n | ‚Äî |
| Protoboard | 1 | Ensamble | ‚Äî |



## Tabla de costos del proyecto ‡ºò‚ãÜ


| Componente                          | Cantidad | Precio unitario | Subtotal | Nota |
|-------------------------------------|----------|------------------|----------|------|
| M√≥dulo MOSFET                       | 1        | $1.200           | $1.200   | Comprado |
| Sensor ultras√≥nico HC-SR04         | 2        | $1.500           | $3.000   | Comprado |
| Motor vibrador (no usado)          | 1        | $2.000           | $2.000   | Comprado pero no utilizado |
| Micro motor N20 DC                 | 1        | $0               | $0       | Pedido al laboratorio |
| LED RGB (pack de 15)               | 15       | $100             | $1.500   | Comprado (pack completo) |
| Cables hembra‚Äìhembra (set)         | 1        | $1.000           | $1.000   | Comprado |
| Arduino UNO                         | 2        | $0               | $0       | Ya los ten√≠an |
| Protoboard                          | 1        | $0               | $0       | Ya la ten√≠an |
| Cable USB                           | 1        | $0               | $0       | Ya lo ten√≠an |
| Servo SG90                          | 1        | $0               | $0       | Laboratorio |
| Resistencias                        | varias   | $0               | $0       | Laboratorio |
| Material de impresi√≥n 3D           | ‚Äî        | $0               | $0       | Laboratorio |
| Silicona y fijaci√≥n                 | ‚Äî        | $0               | $0       | Ya lo ten√≠an |



## **Total gastado realmente:** **$8.700**

> Este total corresponde √∫nicamente al gasto real del grupo.  
> Muchos componentes fueron aportados por el laboratorio o ya estaban en posesi√≥n del equipo.

‚îÄ‚îÄ‚îÄ‚îÄ ‡≠®‡≠ß ‚îÄ‚îÄ‚îÄ‚îÄ

## Instrucciones de uso ‡´Æ‚Çç ¬¥ Íí≥ `‚Çé·Éê

Las m√°quinas est√°n dise√±adas para interactuar **entre ellas**, como dos gatitos hermanos que reaccionan a la presencia del otro. No requieren intervenci√≥n humana m√°s all√° de colocarlas correctamente.

## Preparaci√≥n inicial

1. Conecta ambas m√°quinas a su fuente de alimentaci√≥n (USB o 5V).
2. Enci√©ndelas simult√°neamente.
3. Ub√≠calas **una frente a la otra**, a una distancia aproximada de **20‚Äì40 cm**.
4. Ajusta su posici√≥n hasta que **sus sensores ultras√≥nicos se apunten directamente**, como si ‚Äúse miraran‚Äù.


## M√°quina Ansiedad ‚Äî Comportamiento ‚âΩ^- Àï -^‚âº

1. **Cuando la m√°quina Verg√ºenza se acerca**, la Ansiedad lo interpreta como una activaci√≥n emocional.  
   - Aumenta su vibraci√≥n.  
   - El LED acelera su ritmo.  

2. **Si la m√°quina Verg√ºenza se esconde o gira**, la Ansiedad incrementa su estado nervioso.  
   Reacciona con:  
   - Vibraci√≥n m√°s intensa,  
   - Cambios r√°pidos en el LED,  
   - Un ‚Äútemblor‚Äù que tarda en calmarse.

3. **Cuando Verg√ºenza se aleja**, la Ansiedad no se calma de inmediato:  
   - Mantiene vibraciones residuales.  
   - Su LED sigue agitado unos segundos.

---

## M√°quina Verg√ºenza ‚Äî Comportamiento ‚âΩ^- Àï -^‚âº

1. **Cuando la m√°quina Ansiedad se acerca**, Verg√ºenza reacciona como un gatito t√≠mido:  
   - Se apaga el LED.  
   - El servo gira de forma abrupta para ‚Äúesconderse‚Äù.

2. **Si Ansiedad est√° muy agitada**, Verg√ºenza se retrae a√∫n m√°s.  
   - Mantiene el servo en posici√≥n de ocultamiento.  
   - No vuelve a mostrarse hasta que la otra m√°quina se calme.

3. **Cuando Ansiedad reduce su vibraci√≥n**, Verg√ºenza entra en un periodo de ‚Äúrecuperaci√≥n‚Äù:  
   - No enciende su LED inmediatamente  
   - No vuelve a girar a su posici√≥n original enseguida  
   - Espera unos segundos antes de mostrar se√±ales de calma


## Observaci√≥n de la interacci√≥n ‡´Æ‚Çç¬¥À∂‚Ä¢ . ‚Ä¢ ‚ëÖ ‚Çé·Éê

- Las m√°quinas funcionan como ‚Äúcuerpos emocionales‚Äù: **no responden al humano**, sino **a la presencia del otro**.  
- Puedes acercar o alejar una de la otra **como si fueran dos gatitos interactuando**.  
- No necesitan fuerza manual ni intervenci√≥n directa: su lenguaje es gestual y sutil.


## Recomendaciones ‚Äº

- Evita que los sensores ultras√≥nicos queden muy desalineados.  
- No manipular manualmente el servo o motor.  
- Mantener sobre una superficie estable.  
- Las m√°quinas no deben tener obst√°culos entre ellas.


‚îÄ‚îÄ‚îÄ‚îÄ ‡≠®‡≠ß ‚îÄ‚îÄ‚îÄ‚îÄ

## Contexto de uso ‡πã ‡£≠ ‚≠ë

Las m√°quinas fueron dise√±adas como **dos criaturas emocionales**, inspiradas en el comportamiento de dos gatitos hermanos. No buscan resolver problemas pr√°cticos ni cumplir funciones utilitarias: su prop√≥sito es **mostrar un v√≠nculo emocional** construido √∫nicamente a trav√©s de gestos mec√°nicos y distancia.


## Relaci√≥n entre las m√°quinas ‡πã ‡£≠ ‚≠ë

Cada m√°quina encarna una emoci√≥n distinta:

- **Ansiedad:** se activa, tiembla, reacciona de manera intensa.
- **Verg√ºenza:** se esconde, se repliega, evita la mirada directa.

Aunque son diferentes, est√°n conectadas por un mismo lenguaje corporal:  
**la distancia**.

Ambas dependen de la presencia de la otra para expresar su comportamiento.  
Sin la otra m√°quina, quedan en estado neutro, casi ‚Äúsin identidad emocional‚Äù.


## C√≥mo se da la interacci√≥n ‡πã ‡£≠ ‚≠ë

Las m√°quinas deben estar **una frente a la otra**, como dos animales reconoci√©ndose.  
Su ‚Äúmirada‚Äù no es visual, sino digital: los sensores ultras√≥nicos detectan la proximidad y, a partir de eso, cada m√°quina interpreta la presencia de su compa√±era.

- Cuando **Ansiedad** se acerca con energ√≠a, provoca que **Verg√ºenza** se retraiga.  
- Cuando **Verg√ºenza** se esconde, Ansiedad se inquieta m√°s.  
- Si Ansiedad se calma, Verg√ºenza lentamente vuelve a mostrarse.  

Este intercambio genera un peque√±o ecosistema emocional:  
una relaci√≥n donde **una afecta directamente el estado corporal de la otra**.


> El proyecto no busca imitar la vida, sino capturar un fragmento sencillo de ella:
> la manera en que dos seres pueden alterarse mutuamente, incluso sin tocarse.

‚îÄ‚îÄ‚îÄ‚îÄ ‡≠®‡≠ß ‚îÄ‚îÄ‚îÄ‚îÄ

## Desarrollo del circuito ‚äπ ‡£™ Àñ

Cada m√°quina utiliza un Arduino como controlador principal y un conjunto reducido de componentes que permiten traducir la proximidad en un gesto emocional. Aunque ambas comparten la misma estructura b√°sica, cada una expresa su emoci√≥n con mecanismos diferentes.


## Estructura general del sistema ‚äπ ‡£™ Àñ

Cada m√°quina incluye:

- 1 Arduino UNO R4 MINIMA
- 1 Sensor ultras√≥nico HC-SR04  
- 1 elemento actuador (motor o servo)  
- 1 LED RGB  
- Resistencias y cables de conexi√≥n  

Ambas m√°quinas funcionan de manera independiente, pero se comunican al *leer la distancia de la otra*.  
No existe comunicaci√≥n digital entre ellas: **su interacci√≥n nace √∫nicamente de la lectura del espacio compartido**.


## Circuito M√°quina Ansiedad ‚äπ ‡£™ Àñ

La m√°quina Ansiedad utiliza:

- **Motor DC N20** como actuador principal  
- **M√≥dulo MOSFET** para controlar la potencia del motor  
- **LED RGB** que simula un ‚Äúlatido acelerado‚Äù  
- **Sensor HC-SR04** para medir cercan√≠a de la otra m√°quina  

### Conexiones principales ‚äπ ‡£™ Àñ

#### Sensor ultras√≥nico
- VCC ‚Üí 5V  
- GND ‚Üí GND  
- TRIG ‚Üí Pin digital 9  
- ECHO ‚Üí Pin digital 10  

#### Motor N20 + MOSFET
- Motor ‚Üí Terminales OUT del MOSFET  
- MOSFET IN ‚Üí Pin PWM 5  
- MOSFET VCC ‚Üí 5V  
- MOSFET GND ‚Üí GND  

El MOSFET permite regular la intensidad de vibraci√≥n seg√∫n la distancia detectada.

#### LED RGB
- R ‚Üí Pin PWM 3  
- G ‚Üí Pin PWM 6  
- B ‚Üí Pin PWM 11  
- Cada canal con resistencia de 220‚Äì330Œ©


## Circuito M√°quina Verg√ºenza ‚äπ ‡£™ Àñ

La m√°quina Verg√ºenza utiliza:

- **Servo SG90** como actuador principal  
- **LED RGB** que se apaga cuando se siente observada  
- **Sensor HC-SR04** como ‚Äúmirada‚Äù digital  

### Conexiones principales ‚äπ ‡£™ Àñ

#### Sensor ultras√≥nico
- VCC ‚Üí 5V  
- GND ‚Üí GND  
- TRIG ‚Üí Pin digital 8  
- ECHO ‚Üí Pin digital 7  

#### Servomotor SG90
- Se√±al ‚Üí Pin 5  
- VCC ‚Üí 5V  
- GND ‚Üí GND  

El servo gira entre dos posiciones:
- ‚ÄúNormal‚Äù: LED encendido suave  
- ‚ÄúOculta‚Äù: LED apagado, servo girado  

#### LED RGB
- R ‚Üí Pin PWM 3  
- G ‚Üí Pin PWM 6  
- B ‚Üí Pin PWM 11  
- Cada canal con resistencia 220‚Äì330Œ©

‚îÄ‚îÄ‚îÄ‚îÄ‡≠®‡ßé‚îÄ‚îÄ‚îÄ‚îÄ


## Links de compra ‡πã ‡£≠ ‚≠ë

A continuaci√≥n se incluyen los enlaces de compra mencionados en el proceso del taller y presentes en los documentos del proyecto.

### M√≥dulo MOSFET 15A / 400W PWM  
https://afel.cl/products/modulo-mosfet-15a-400w-pwm

### Motor vibrador 
https://afel.cl/products/motor-vibrador-pwm-switch-dc

### Cables HH
https://afel.cl/products/pack-20-cables-de-conexion-hembra-hembra?_pos=2&_sid=79ff9b6a9&_ss=r

### Sensores ultras√≥nicos HC-SR04  
https://afel.cl/products/sensor-de-ultrasonico-hc-sr04

### LED RGB 5mm  
https://afel.cl/products/diodo-led-rgb-5mm


## C√≥digo fuente completo ‡πã ‡£≠ ‚≠ë

A continuaci√≥n se deja un espacio para incluir los c√≥digos completos de ambas m√°quinas.

## C√≥digo M√°quina Ansiedad 
```cpp
/*
  M√°quina de la Ansiedad 
  ---------------------------------------------------------
  Esta m√°quina representa un estado de "ansiedad" usando un 
  motor N20 y un LED RGB. Percibe la distancia con un sensor 
  ultras√≥nico HC-SR04 y reacciona seg√∫n qu√© tan cerca est√© 
  un objeto o la otra m√°quina.

  Mientras m√°s lejos est√° algo ‚Üí vibraci√≥n suave y luz morada tenue.  
  Mientras m√°s se acerca ‚Üí los movimientos y el parpadeo se vuelven 
  m√°s r√°pidos e intensos.  
  A muy poca distancia (‚â§ 5 cm) ‚Üí entra en un estado de caos total.

  No se comunica con otras placas: todas las reacciones dependen 
  √∫nicamente de la distancia.

  Comportamientos:
  - LED RGB √Ånodo Com√∫n ‚Üí SIEMPRE morado (solo R+B).
  - A m√°s distancia ‚Üí calma, ‚Äúlatido‚Äù lento.
  - A medida que te acercas (<50 cm) ‚Üí vibraci√≥n + parpadeo aumentan.
  - A menos de 5 cm ‚Üí caos total.

  Hardware:
  - HC-SR04:
      TRIG = D2
      ECHO = D3
  - Motor N20 con MOSFET ‚Üí PWM en D5
  - LED RGB √Ånodo Com√∫n:
      R = D9
      G = D10 (no se usa)
      B = D11

  Importante:
  - LED √°nodo com√∫n ‚Üí 255 = apagado / 0 = brillo m√°ximo.
*/
// Biblioteca base de Arduino con funciones esenciales
#include <Arduino.h>

// Pin TRIG del sensor ultras√≥nico HC-SR04
#define TRIG 2
// Pin ECHO del sensor ultras√≥nico HC-SR04
#define ECHO 3
// Pin PWM donde est√° conectado el motor N20 mediante MOSFET
#define MOTOR 5

// Canal rojo del LED RGB (√°nodo com√∫n)
#define LED_R 9
// Canal verde del LED RGB (no se usa)
#define LED_V 10
// Canal azul del LED RGB
#define LED_A 11

// Distancia l√≠mite donde empieza el comportamiento reactivo (50 cm)
const int DIST_MAX = 50;
// Valor inicial del filtro suavizado de distancia
long distanciaFiltrada = 50;

// -----------------------------------------------------------
// Funci√≥n que mide la distancia usando el HC-SR04

long medirDistancia() {
  // Asegura el TRIG en LOW antes de enviar el pulso
  digitalWrite(TRIG, LOW);
  // Pausa muy corta para estabilizar
  delayMicroseconds(2);
  // Env√≠a pulso HIGH para activar el disparo del sensor
  digitalWrite(TRIG, HIGH);
  // Mantiene el pulso 10 us, obligatorio para el HC-SR04
  delayMicroseconds(10);
  // Termina el pulso regres√°ndolo a LOW
  digitalWrite(TRIG, LOW);

  // Mide cu√°nto tiempo el pin ECHO permanece HIGH
  long duracion = pulseIn(ECHO, HIGH, 30000);
  // Si no hay lectura, devuelve -1 como error
  if (duracion == 0) return -1;

  // Convierte duraci√≥n a distancia en cm (velocidad del sonido)
  long distancia = duracion * 0.0343 / 2;
  // Si la lectura no es realista, la descarta
  if (distancia < 2 || distancia > 400) return -1;

 // Devuelve finalmente la distancia v√°lida
  return distancia;
}

// -----------------------------------------------------------
// Filtro de suavizado

long filtrar(long nueva) {
  // Si el valor es inv√°lido, conserva el √∫ltimo bueno
  if (nueva < 0) return distanciaFiltrada;
  // Mezcla 70% vieja + 30% nueva
  distanciaFiltrada = (0.7 * distanciaFiltrada) + (0.3 * nueva); 
  // Devuelve la distancia ya suavizada
  return distanciaFiltrada;
}

// -----------------------------------------------------------
// Funci√≥n para controlar la vibraci√≥n del motor con jitter 
   // Se usa jitter (variaci√≥n aleatoria controlada) para evitar que el movimiento sea mec√°nico
   // y repetitivo. Esto simula temblores naturales, evita patrones r√≠gidos y hace que el
   // comportamiento se sienta m√°s org√°nico, especialmente cuando la distancia var√≠a cerca
   // de los l√≠mites del sensor.

void vibrar(int intensidad, int tiempo) {
  // Asegura que est√© dentro del rango v√°lido PWM
  intensidad = constrain(intensidad, 0, 255);

  // Genera ruido aleatorio para que la vibraci√≥n no sea perfecta
  int jitter = random(-15, 15);
  // Aplica el jitter dentro de l√≠mites permitidos   
  int power = constrain(intensidad + jitter, 0, 255);

// Enciende el motor con la intensidad seleccionada
  analogWrite(MOTOR, power);
  // Mantiene la vibraci√≥n el tiempo indicado
  delay(tiempo);
  // Apaga el motor despu√©s del pulso
  analogWrite(MOTOR, 0); 
}

// -----------------------------------------------------------
// Funci√≥n que hace parpadear el LED en color morado

void parpadeoLED(int intensidad, int tiempo) {
  // Inversi√≥n necesaria en LEDs de √°nodo com√∫n
  int brightness = 255 - intensidad;

  // Enciende canal rojo con intensidad invertida
  analogWrite(LED_R, brightness);
  // Enciende canal azul con intensidad invertida
  analogWrite(LED_A, brightness);
  // Mantiene el verde apagado siempre (morado = rojo + azul)
  analogWrite(LED_V, 255);

  // Mantiene el LED prendido el tiempo indicado
  delay(tiempo);

  // Apaga el canal rojo (255 en √°nodo com√∫n = apagado)
  analogWrite(LED_R, 255);
  // Apaga canal azul
  analogWrite(LED_A, 255);
  // Peque√±a pausa para simular parpadeo
  delay(tiempo * 0.6);
}

// -----------------------------------------------------------
// Configuraci√≥n inicial

void setup() {
  // TRIG como salida
  pinMode(TRIG, OUTPUT);
  // ECHO como entrada
  pinMode(ECHO, INPUT);

  // Pin del motor como salida PWM
  pinMode(MOTOR, OUTPUT);

  // Canales del LED RGB como salida
  pinMode(LED_R, OUTPUT);
  pinMode(LED_V, OUTPUT);
  pinMode(LED_A, OUTPUT);

  // Semilla de n√∫meros aleatorios usando ruido del pin anal√≥gico
  // Usamos el ruido el√©ctrico del pin anal√≥gico como semilla para random(), as√≠ la secuencia
  // de n√∫meros aleatorios cambia realmente cada vez que se enciende el Arduino.
  randomSeed(analogRead(A0));
}

// -----------------------------------------------------------
// Bucle principal: reacciona seg√∫n la distancia medida

void loop() {
  // Mide distancia actual
  long lectura = medirDistancia();
  // Suaviza esa distancia con el filtro
  long d = filtrar(lectura);
  // Si hay error de lectura
  if (lectura < 0) {
  // Apaga motor por seguridad
    analogWrite(MOTOR, 0);
  // LED indica estado de error con parpadeo suave
    parpadeoLED(30, 150);
  // Reinicia ciclo
    return;
  }

  // ---------------------------------------------------------
  // CASO 1: OBJETO LEJOS

  if (d > DIST_MAX) {
    // Vibraci√≥n suave
    vibrar(20, 60);
    // Parpadeo lento
    parpadeoLED(40, 120);

    // A veces hace un pico aleatorio
    if (random(0, 10) == 0) {
      vibrar(40, 20);
      parpadeoLED(80, 40);
    }
    // Peque√±a pausa
    delay(80);
    // Termina ciclo
    return;
  }

  // ---------------------------------------------------------
  // CASO 2: DISTANCIA INTERMEDIA (30‚Äì50 cm)

  if (d > 30 && d <= DIST_MAX) {
    // M√°s cerca ‚Üí m√°s intensidad
    int intensidad = map(d, DIST_MAX, 30, 60, 150);
    // M√°s cerca ‚Üí menos pausa
    int pausa = map(d, DIST_MAX, 30, 200, 80);

    // Vibraci√≥n principal
    vibrar(intensidad, 40);
    // Parpadeo sincronizado
    parpadeoLED(intensidad, 50);

    // Segundo pulso m√°s ca√≥tico
    vibrar(intensidad + random(-20, 30), 25);
    parpadeoLED(intensidad + random(-30, 40), 30);

    // Pausa total del ciclo
    delay(pausa);
    return;
  }

  // ---------------------------------------------------------
  // CASO 3: DISTANCIA CERCANA (5‚Äì30 cm)

  if (d > 5 && d <= 30) {
    // Intensidad crece al acercarse
    int intensidad = map(d, 30, 5, 100, 255);
    // Ciclo se vuelve m√°s r√°pido
    int rapidez = map(d, 30, 5, 80, 20);

    // Vibraci√≥n principal
    vibrar(intensidad, 30);
    // Parpadeo acorde
    parpadeoLED(intensidad, 25);

    // Variaciones ca√≥ticas
    vibrar(intensidad - random(0, 60), random(10, 40));
    parpadeoLED(intensidad - random(0, 70), random(15, 40));

    // Ciclo acelerado
    delay(rapidez);
    return;
  }

  // ---------------------------------------------------------
  // CASO 4: CAOS TOTAL (‚â§ 5 cm)

  if (d <= 5) {
    // Vibraci√≥n m√°xima
    vibrar(255, 20);
    // Parpadeo m√°ximo
    parpadeoLED(255, 20);

    // Pulso irregular
    vibrar(180 + random(-40, 40), 15);
    // Luz muy inestable
    parpadeoLED(200 + random(-50, 50), 15);

    // Remate final
    vibrar(255, 10);
    parpadeoLED(255, 10);
    return;
  }
}

```

## C√≥digo M√°quina Verg√ºenza
```cpp
/*
  M√°quina de la Verg√ºenza
  ---------------------------------------------------------
  Esta m√°quina representa un estado de ‚Äúverg√ºenza‚Äù usando 
  un servo SG90 y un LED RGB. Percibe la distancia mediante 
  un sensor ultras√≥nico HC-SR04 y reacciona dependiendo de 
  qu√© tan cerca est√© un objeto o la otra m√°quina.

  A mayor distancia ‚Üí parpadeo lento y servo quieto.  
  A medida que te acercas (< 50 cm) ‚Üí el parpadeo acelera y 
  el servo oscila con mayor amplitud.  
  A muy poca distancia (‚â§ 5 cm) ‚Üí entra en un estado de caos.

  No se comunica con otras placas: todas las reacciones dependen
  solo de la distancia.

  Comportamientos:
  // LED cian (verde + azul)
  // LED RGB √Ånodo Com√∫n ‚Üí SIEMPRE cian (solo V+B).

  - Lejos ‚Üí calma, parpadeo lento.
  - Cercan√≠a ‚Üí servo inquieto + parpadeo r√°pido.
  - Muy cerca (‚â§ 5 cm) ‚Üí caos total.

  Hardware:
  - HC-SR04:
      TRIG = D2
      ECHO = D3
  - LED RGB √Ånodo Com√∫n:
      R = D9
      G = D10
      B = D11
  - Servo SG90 ‚Üí D6

  Importante:
  - LED √°nodo com√∫n ‚Üí LOW = encendido / HIGH = apagado.
*/

#include <Servo.h> // Biblioteca para controlar el servo SG90

// Pin TRIG del sensor ultras√≥nico HC-SR04
#define TRIG 2
// Pin ECHO del sensor ultras√≥nico HC-SR04
#define ECHO 3

// Canal rojo del LED RGB (√°nodo com√∫n)
#define LED_R 9
// Canal verde del LED RGB
#define LED_V 10
// Canal azul del LED RGB
#define LED_A 11

// Pin donde est√° conectado el servo SG90
#define SERVO_PIN 6

// Distancia donde empieza a reaccionar (50 cm)
const int DIST_MAX = 50;
// Distancia m√≠nima real v√°lida (5 cm)
const int DIST_MIN = 5;

// Objeto que controla el servo
Servo servoVerguenza;

// Variables internas del movimiento del servo
int centro = 90;        // Punto neutro del servo
int angulo = 90;        // √Ångulo actual del servo
int direccion = 1;      // Hacia d√≥nde se mueve el servo (1 o -1)


// -----------------------------------------------------------
// Configuraci√≥n inicial

void setup() {
  // TRIG como salida
  pinMode(TRIG, OUTPUT);
  // ECHO como entrada
  pinMode(ECHO, INPUT);

  // LED RGB como salida
  pinMode(LED_R, OUTPUT);
  pinMode(LED_V, OUTPUT);
  pinMode(LED_A, OUTPUT);

  // El LED se encender√° con verde + azul ‚Üí color cian.
  
  digitalWrite(LED_R, HIGH); // Rojo siempre apagado (no participa en el color)
  digitalWrite(LED_V, HIGH); // Verde apagado hasta parpadeo (LOW = encendido)
  digitalWrite(LED_A, HIGH); // Azul apagado hasta parpadeo

  // Inicializa el servo y lo centra
  servoVerguenza.attach(SERVO_PIN);
  servoVerguenza.write(90);

  // Serial para depuraci√≥n opcional
  Serial.begin(9600);
}


// -----------------------------------------------------------
// Mide distancia usando el HC-SR04

long medirDistancia() {
  // Asegura TRIG en LOW antes del pulso
  digitalWrite(TRIG, LOW);
  delayMicroseconds(2);

  // Pulso HIGH de 10 us (activaci√≥n del HC-SR04)
  digitalWrite(TRIG, HIGH);
  delayMicroseconds(10);
  digitalWrite(TRIG, LOW);

  // Duraci√≥n del pulso en ECHO
  long duracion = pulseIn(ECHO, HIGH, 30000);

  // Si no hay lectura ‚Üí error
  if (duracion == 0) return -1;

  // Conversi√≥n a cent√≠metros
  long distancia = duracion * 0.0343 / 2;

  // Filtra lecturas irreales
  if (distancia < 2 || distancia > 400) return -1;

  return distancia;
}


// -----------------------------------------------------------
// Parpadeo del LED en color cian (verde + azul)

void parpadeoLED(int tiempo) {
  //verde + azul encendidos ‚Üí cian

  digitalWrite(LED_V, LOW); // Enciende verde (LOW = encendido)
  digitalWrite(LED_A, LOW); // Enciende azul  (LOW = encendido)

  // Mantiene encendido el tiempo indicado
  delay(tiempo);

  // Apaga los canales (HIGH = apagado)
  digitalWrite(LED_V, HIGH);
  digitalWrite(LED_A, HIGH);

  // Pausa antes del siguiente ciclo
  delay(tiempo * 0.6);
}


// -----------------------------------------------------------
// Movimiento del servo seg√∫n amplitud y velocidad

void moverServo(int amplitud, int velocidad) {
  // Aumenta o disminuye √°ngulo seg√∫n direcci√≥n
  angulo += direccion;

  // Invierte direcci√≥n cuando llega a los l√≠mites
  if (angulo >= centro + amplitud) direccion = -1;
  if (angulo <= centro - amplitud) direccion = 1;

  // Env√≠a posici√≥n al servo
  servoVerguenza.write(angulo);

  // Pausa que define velocidad del movimiento
  delay(velocidad);
}


// -----------------------------------------------------------
// Bucle principal

void loop() {
  // Mide la distancia actual
  long d = medirDistancia();

  // Si la lectura es inv√°lida
  if (d < 0) {
    // LED indica error con parpadeo suave
    parpadeoLED(150);
    // Servo queda quieto
    servoVerguenza.write(90);
    return;
  }

  // ---------------------------------------------------------
  // CASO 1: OBJETO LEJOS (distancia > 50 cm)

  if (d > DIST_MAX) {
    // Parpadeo lento ‚Üí calma
    parpadeoLED(200);
    // Servo en posici√≥n neutra
    servoVerguenza.write(90);
    return;
  }

  // ---------------------------------------------------------
  // CASO 2: DISTANCIA INTERMEDIA (5‚Äì50 cm)

  if (d > DIST_MIN && d <= DIST_MAX) {
    // A menor distancia ‚Üí parpadeo m√°s r√°pido
    int tLed = map(d, DIST_MAX, DIST_MIN, 300, 20);
    tLed = constrain(tLed, 20, 300);

    // Parpadeo cian
    parpadeoLED(tLed);

    // A menor distancia ‚Üí mayor amplitud
    int amplitud = map(d, DIST_MAX, DIST_MIN, 5, 50);
    amplitud = constrain(amplitud, 5, 50);

    // A menor distancia ‚Üí m√°s velocidad
    int velocidad = map(d, DIST_MAX, DIST_MIN, 40, 5);
    velocidad = constrain(velocidad, 5, 40);

    // Mueve el servo seg√∫n cercan√≠a
    moverServo(amplitud, velocidad);
    return;
  }

  // ---------------------------------------------------------
  // CASO 3: CAOS TOTAL (‚â§ 5 cm)

  if (d <= DIST_MIN) {
    // Movimiento aleatorio entre 60¬∞ y 120¬∞
    int ang = random(60, 120);
    servoVerguenza.write(ang);

    // LED parpadea muy r√°pido (cian)
    parpadeoLED(20);

    // Peque√±a pausa
    delay(5);
    return;
  }
}

```
